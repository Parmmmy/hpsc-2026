#include <iostream>
#include <typeinfo>
#include <random>
#include <stdint.h>
#include <cublas_v2.h>
#include <mma.h>
#include <chrono>
using namespace std;
using namespace nvcuda;

constexpr int BM = 128;
constexpr int BN = 128; 
constexpr int BK = 16;
constexpr int WARP_M = 32; 
constexpr int WARP_N = 64; 
constexpr int PAD = 8; 

#define SMEM_A(stage, row, col) smem_a[(stage)][(row)][(col)]
#define SMEM_B(stage, row, col) smem_b[(stage)][(row)][(col)]


__global__ __launch_bounds__(256, 2)
void kernel(int dim_m, int dim_n, int dim_k, const float* __restrict__ d_a, const float* __restrict__ d_b, float* __restrict__ d_c) 
{

    const int bm = blockIdx.x * BM;  
    const int bn = blockIdx.y * BN;   

    const int tid = threadIdx.x;
    const int warp_id = tid / 32;
    // const int lane = tid & 31;

    constexpr int WARPS_N = BN / WARP_N;
    const int wr = warp_id / WARPS_N;
    const int wc = warp_id % WARPS_N;

    __shared__ half smem_a[2][BK][BM + PAD]; 
    __shared__ half smem_b[2][BK][BN + PAD]; 

    wmma::fragment<wmma::accumulator, 16, 16, 16, float> acc[2][4];
    #pragma unroll
    for (int r = 0; r < 2; r++)
        #pragma unroll
        for (int c = 0; c < 4; c++)
            wmma::fill_fragment(acc[r][c], 0.0f);

     int k0 = 0;

    {
        
        #pragma unroll
        for (int idx = tid; idx < BK * BM; idx += 256) {
            int k_local = idx / BM;
            int m_local = idx % BM;
            int g_k = k0 + k_local;
            int g_m = bm + m_local;
            smem_a[0][k_local][m_local] = __float2half(d_a[g_k * dim_m + g_m]);
        }
        #pragma unroll
        for (int idx = tid; idx < BK * BN; idx += 256) {
            int k_local = idx / BN;
            int n_local = idx % BN;
            int g_k = k0 + k_local;
            int g_n = bn + n_local;
            smem_b[0][k_local][n_local] = __float2half(d_b[g_k * dim_n + g_n]);
        }
    }
    __syncthreads();

    for (int k = 0; k < dim_k; k += BK) {
        int cur  = (k / BK) & 1;
        int next = cur ^ 1;

        if (k + BK < dim_k) {
            int kn = k + BK;
            #pragma unroll
            for (int idx = tid; idx < BK * BM; idx += 256) {
                int k_local = idx / BM;
                int m_local = idx % BM;
                int g_k = kn + k_local;
                int g_m = bm + m_local;
                smem_a[next][k_local][m_local] =__float2half(d_a[g_k * dim_m + g_m]);
            }
            #pragma unroll
            for (int idx = tid; idx < BK * BN; idx += 256) {
                int k_local = idx / BN;
                int n_local = idx % BN;
                int g_k = kn + k_local;
                int g_n = bn + n_local;
                smem_b[next][k_local][n_local] =__float2half(d_b[g_k * dim_n + g_n]);
            }
        }

        #pragma unroll
        for (int r = 0; r < 2; r++) {
            wmma::fragment<wmma::matrix_a, 16, 16, 16, half, wmma::col_major> a_frag;
            wmma::load_matrix_sync(a_frag,
                &SMEM_A(cur, 0, wr * WARP_M + r * 16),
                BM + PAD);
            #pragma unroll
            for (int c = 0; c < 4; c++) {
                wmma::fragment<wmma::matrix_b, 16, 16, 16, half, wmma::row_major> b_frag;
                wmma::load_matrix_sync(b_frag,
                    &SMEM_B(cur, 0, wc * WARP_N + c * 16),
                    BN + PAD);
                wmma::mma_sync(acc[r][c], a_frag, b_frag, acc[r][c]);
            }
        }
        __syncthreads();
    }

    #pragma unroll
    for (int r = 0; r < 2; r++) {
        #pragma unroll
        for (int c = 0; c < 4; c++) {
            int c_m = bm + wr * WARP_M + r * 16;
            int c_n = bn + wc * WARP_N + c * 16;
            if (c_m < dim_m && c_n < dim_n)
                wmma::store_matrix_sync(&d_c[c_n * dim_m + c_m],
                                        acc[r][c], dim_m,
                                        wmma::mem_col_major);
        }
    }
}



int main(int argc, const char **argv) {
  int m = 10240;
  int k = 4096;
  int n = 8192;
  float alpha = 1.0;
  float beta = 0.0;
  int Nt = 10;
  float *A, *B, *C, *C2;
  cudaMallocManaged(&A, m * k * sizeof(float));
  cudaMallocManaged(&B, k * n * sizeof(float));
  cudaMallocManaged(&C, m * n * sizeof(float));
  cudaMallocManaged(&C2, m * n * sizeof(float));
  for (int i=0; i<m; i++)
    for (int j=0; j<k; j++)
      A[k*i+j] = drand48();
  for (int i=0; i<k; i++)
    for (int j=0; j<n; j++)
      B[n*i+j] = drand48();
  for (int i=0; i<n; i++)
    for (int j=0; j<m; j++)
      C[m*i+j] = C2[m*i+j] = 0;



  cublasHandle_t cublas_handle;
  cublasCreate(&cublas_handle);
  auto tic = chrono::steady_clock::now();
  for (int i = 0; i < Nt+2; i++) {
    if (i == 2) tic = chrono::steady_clock::now();
    cublasGemmEx(cublas_handle,
		 CUBLAS_OP_N,
		 CUBLAS_OP_N,
		 m,
		 n,
		 k,
		 &alpha,
		 A, CUDA_R_32F, m,
		 B, CUDA_R_32F, k,
		 &beta,
		 C, CUDA_R_32F, m,
		 CUBLAS_COMPUTE_32F_FAST_16F,
		 CUBLAS_GEMM_DEFAULT_TENSOR_OP);
    cudaDeviceSynchronize();
  }
  auto toc = chrono::steady_clock::now();
  int64_t num_flops = (2 * int64_t(m) * int64_t(n) * int64_t(k)) + (2 * int64_t(m) * int64_t(n));
  double tcublas = chrono::duration<double>(toc - tic).count() / Nt;
  double cublas_flops = double(num_flops) / tcublas / 1.0e9;




 dim3 block(256);
   dim3 grid((m + BM - 1) / BM, (n + BN - 1) / BN);

  for (int i = 0; i < Nt+2; i++) {
    if (i == 2) tic = chrono::steady_clock::now();
    kernel<<< grid, block >>>(m,
			      n,
			      k,
			      A,
			      B,
			      C2);
    cudaDeviceSynchronize();
  }
  toc = chrono::steady_clock::now();
  double tcutlass = chrono::duration<double>(toc - tic).count() / Nt;
  double cutlass_flops = double(num_flops) / tcutlass / 1.0e9;
  printf("CUBLAS: %.2f Gflops, CUTLASS: %.2f Gflops\n", cublas_flops, cutlass_flops);



  double err = 0;
  for (int i=0; i<n; i++) {
    for (int j=0; j<m; j++) {
      err += fabs(C[m*i+j] - C2[m*i+j]);
    }
  }
  printf("error: %lf\n", err/n/m);
  cudaFree(A);
  cudaFree(B);
  cudaFree(C);
  cudaFree(C2);
  cublasDestroy(cublas_handle);
}
